---
layout: post
title: FPGA学习（-）
data: 2022-06-07
description: "FPGA学习笔记"
tag: FPGA
typora-root-url: ..
---


## 前言

因为不懂FPGA部分，会导致很多事情效率很低，并且会把事情想的很简单，对接的接口无法成功的合作起来，因此在闲暇的时候开始学习FPGA的部分内容。主要使用自己公司的板子，做好笔记。

## 语法

### Verilog的数据类型

Verilog的数据类型主要有三大类数据类型，即寄存器类型、线网类型和参数类型。也就是说，在数字电路中起作用的数据类型应该是寄存器类型和线网类型。

**寄存器类型：**寄存器类型表示一个抽象的数据储存单元，它只能在always语句和initial语句中被赋值，并且它的值从一个赋值到另一个赋值过程中被保存下来。如果该过程语句描述的是时序逻辑，即always语句带有时钟信号，则该寄存器变量对应为寄存器；如果该过程语句描述的是组合逻辑，即always语句不带有时钟信号，则该寄存器变量对应为硬件连线；寄存器类型的缺省值是x。

寄存器数据类型有很多种，如reg、integer、real等，其中最常用的就是reg类型，它的使用方法如下：

```verilog
//reg define
reg [31:0]	delay_cnt;		//延时计数器
reg key_flag;			//按键标志
```

**线网类型：**线网表示Verilog结构化元件间的物理连线。它的值由驱动元件的值决定，例如连续赋值或门的输出。如果没有驱动元件连接到线网，线网的缺省值为z（高阻态）。使用方法如下：

```verilog
wire	data_en;			//数据使能信号
wire	[7:0]	data;		//数据
```

**参数类型:**参数其实就是一个常量，常被用于定义状态机的状态、数据位宽和延迟大小等，由于它可以在编译时修改参数的值，因此它又常被用于一些参数可调的模块中，使用户在实例化模块时，可以根据需要配置参数。在定义参数时，我们可以一次定义多个参数,参数与参数之间需要用逗号隔开。这里我们需要注意的是参数的定义是局部的,只在当前模块中有效。它的使用方法如下:

```verilog
parameter DATA_WIDTH=8; //数据位宽为8位
```

### Verilog的运算符

verilog的运算符按照功能一般可以分为7中运算符，分别是

1. 算术运算符 + - * / %
2. 关系运算符  > < >= <= == !=
3. 逻辑运算符  ! && ||  非与或
4. 条件运算符  ?:  a? b:c 如果a为真，就选择b，否则选择c
5. 位运算符     ~ 将a的每个位进行取反
6. 移位运算符  a<< b  a >> b 
7. 拼接运算符  {a,b} 将a和b拼接起来作为一个新信号

### Verilog常用关键字

|     关键字      |           含义           |
| :-------------: | :----------------------: |
|     module      |       模块开始定义       |
|      input      |       输入端口定义       |
|     output      |       输出端口定义       |
|      inout      |       双向端口定义       |
|    parameter    |      信号的参数定义      |
|      wire       |       wire信号定义       |
|       reg       |       reg信号定义        |
|     always      | 产生reg信号语句的关键字  |
|     assign      | 产生wire信号语句的关键字 |
|      begin      |      语句的起始标志      |
|       end       |      语句的结束标志      |
| posedge/negedge |      时序电路的标志      |
|      case       |     Case语句起始标志     |
|     default     |  Case语句的默认分支标志  |
|     endcase     |     Case语句结束标记     |
|       if        |     if/else语句标记      |
|      else       |     if/else语句标记      |
|       for       |       for语句标记        |
|    endmodule    |       模块结束定义       |

### 程序框架

用LED闪烁来展示吧。

```verilog
`timescale 1ns / 1ps

module led(
        input       sys_clk,
        input       sys_rst_n,
        output reg [3:0] led  //4bit led
     );

     parameter WIDTH = 25 ;
     parameter COUNT_MAX = 25_000_000;      //50M时钟 = 20ns， 0.5s/20ns = 25_000_000,需要25bit位宽

     reg [WIDTH-1:0] counter ;
     reg [1:0] led_ctrl_cnt;

     wire counter_en;

    /*
    *main code
    */
    //计数到最大值时产生高电平使能信号
    assign counter_en = (counter == (COUNT_MAX - 1'b1)) ? 1'b1:1'b0;

    //用于产生0.5s信号计时器
    always @(posedge sys_clk or negedge sys_rst_n) begin
        if(sys_rst_n == 1'b0)
            counter <= 1'b0;
        else if(counter_en)
            counter <= 1'b0;
        else
            counter <= counter + 1'b1;
    end

    //led流水控制计数器
    always @(posedge sys_clk or negedge sys_rst_n) begin
        if(sys_rst_n == 1'b0)
            led_ctrl_cnt <= 2'b0;
        else  if (counter_en)
             led_ctrl_cnt <= led_ctrl_cnt + 2'b1;
    end

    //控制IO口的高低电平实现发光二极管的亮灭
    always @(posedge clk or negedge sys_rst_n) begin
        if(sys_rst_n == 1'b0)
            led <= 4'b0;
        else begin
          case(led_ctrl_cnt)
            2'd0:  led <= 4'b0001;
            2'd1:  led <= 4'b0010;
            2'd2:  led <= 4'b0100;
            2'd3:  led <= 4'b1000;
            default : ;
          endcase;
        end
    end

endmodule

```

### Verilog高级知识点

上面主要是介绍了Verilog一些基础的知识点和程序框架，这里主要介绍高级知识点。高级知识点包括阻塞赋值和非阻塞赋值、assign和always语句差异、什么是锁存器、状态机、模块化设计等。

阻塞赋值就是说，在一个always块中，后面的语句会受到前语句的影响，具体来说在同一个always中，一条阻塞赋值语句如果没有执行结束，那么该语句后面的语句就不能被执行，即被“阻塞”。也就是说always是一种顺序关系。

而非阻塞赋值是由时钟节拍决定的，begin-end之间的所有的语句是一起执行且一种时钟只执行一次，属于并行执行语句。

关于什么时候用阻塞赋值什么时候使用非阻塞赋值？

- 在描述组合逻辑电路的时候，使用阻塞赋值，比如assign赋值语句和不带时钟的always赋值语句，这种电路结构只与输入电平的变化有关系，代码如下：

  ```verilog
  /* assign赋值语句 */
  assign data = (data_en == 1'b1) ? 8'd255:8'd0;
  ```

  ```verilog
  /* 不带时钟的always语句 */
  always@(*)begin
      if(en)begin
      	a = a0;
          b = b0;
      end
      else begin
          a = a1;
          b = b1;
      end
  end
  ```

- 在描述时序逻辑的时候,使用非阻塞赋值,综合成时序逻辑的电路结构,比如带时钟的 always 语句;这种电路结构往往与触发沿有关系,只有在触发沿时才可能发生赋值的变化,代码如下:

  ```verilog
  always @(posedge sys_clk or negedge sys_rst_n) begin
      if (!sys_rst_n) begin
          a <= 1'b0;
          b <= 1'b0;
      end
      else begin
          a <= c;
          b <= d;
      end
  end
  ```

  

#### assign和always区别

- assign 语句和 always 语句是 Verilog 中的两个基本语句,这两个都是经常使用的语句。

- assign 语句使用时不能带时钟。

- always 语句可以带时钟,也可以不带时钟。在 always 不带时钟时,逻辑功能和 assign 完全一致,都是只产生组合逻辑。比较简单的组合逻辑推荐使用 assign 语句,比较复杂的组合逻辑推荐使用 always 语句。示例如下:

  ```verilog
  assign counter_en = (counter == (COUNT_MAX - 1'B1)) ? 1'b1 : 1'b0;
  always @(*) begin
      case(led_ctrl_cnt)
      	2'd0	:	led = 4'b0001;
          2'd1	:	led = 4'b0010;
          2'd2	:	led = 4'b0100;
          2'd3	:	led = 4'b1000;
          default: led = 4'b0000;
      endcase
  end
  ```

#### 带时钟和不带时钟的always

always 语句可以带时钟,也可以不带时钟。在 always 不带时钟时,逻辑功能和 assign 完全一致,虽然产生的信号定义还是 reg 类型,但是该语句产生的还是组合逻辑。

```verilog
reg [3:0] led;
always @(*) begin
    case(led_ctrl_cnt)
    	2'd0	:	led = 4'b0001;
        2'd1	:	led = 4'b0010;
        2'd2	:	led = 4'b0100;
        2'd3	:	led = 4'b1000;
        default: led = 4'b0000;
    endcase
end
```

在always带时钟信号时，这个逻辑语句才能产生真正的寄存器，如下实例counter就是真正的寄存器。

```verilog
//用于产生0.5s使能信号的计数器
always @(posedge sys_clk or negedge sys_rst_n) begin
    if(sys_rst_n == 1'b0)
        counter <= 1'b0;
    else if(counter_en)
        counter <= 1'b0;
    else
        counter <= counter + 1'b1;
end
```

#### Latch(锁存器)

latch 是指锁存器,是一种对脉冲电平敏感的存储单元电路。锁存器和寄存器都是基本存储单元,锁存器是电平触发的存储器,寄存器是边沿触发的存储器。两者的基本功能是一样的,都可以存储数据。锁存器是组合逻辑产生的,而寄存器是在时序电路中使用,由时钟触发产生的。

latch 的主要危害是会产生毛刺(glitch),这种毛刺对下一级电路是很危险的。并且其隐蔽性很强,不易查出。因此,在设计中,应尽量避免 latch 的使用。

代码里面出现 latch 的两个原因是在组合逻辑中,if 或者 case 语句不完整的描述,比如 if 缺少 else 分支,case 缺少 default 分支,导致代码在综合过程中出现了 latch。解决办法就是 if 必须带 else 分支,case必须带 default 分支。**大家需要注意下,只有不带时钟的 always 语句 if 或者 case 语句不完整才会产生 latch,带时钟的语句if 或者 case 语句不完整描述不会产生 latch。**

#### 状态机

Verilog 是硬件描述语言,硬件电路是并行执行的,当需要按照流程或者步骤来完成某个功能时,代码中通常会使用很多个 if 嵌套语句来实现,这样就增加了代码的复杂度,以及降低了代码的可读性,这个时候就可以使用状态机来编写代码。状态机相当于一个控制器,它将一项功能的完成分解为若干步,每一步对应于二进制的一个状态,通过预先设计的顺序在各状态之间进行转换,状态转换的过程就是实现逻辑功能的过程。

状态机，全称是有限状态机（Finite State Machine，FSM），是一个在有限个状态之间按一定规律转换的时序电路，可以认为是组合逻辑和时序逻辑的一种组合。状态机通过控制各个状态的跳转来控制流程，使得整个代码看上去更加清晰易懂，在控制复杂流程的时候，状态机优势明显，因此基本上都会用到状态机。如SDRAM控制器。

一般根据状态机的输出是否与输入条件相关，可将状态机分为两大类，即摩尔（More）型状态机和米勒（Melay）型状态机。

- Mealy状态机：组合逻辑的输出不仅取决与当前状态，还取决于输入状态。
- Moore状态机：组合逻辑的输出只取决于当前状态。

具体使用方法，后续实例中会介绍到。

#### 模块化设计

模块化设计是FPGA设计中一个很重要的技巧，它能够使一个大型设计的分工协作、仿真测试更加容易，代码维护或升级更加便利，当更改某个子模块时，不会影响到其它模块的实现结果、进行模块化、标准化设计的最终目的就是提高设计的通用性，减少不同项目中同一功能设计和验证引入的工作量。划分模块的基本原则是子模块功能相对独立、模块内部联系尽量紧密、模块间的连接尽量简单。

在进行模块化设计中,对于复杂的数字系统,我们一般采用自顶向下的设计方式。可以把系统划分成几个功能模块,每个功能模块再划分成下一层的子模块;每个模块的设计对应一个 module,一个 module设计成一个 Verilog 程序文件。因此,对一个系统的顶层模块,我们采用结构化的设计,即顶层模块分别调用了各个功能模块。下图是模块化设计的功能框图,一般整个设计的顶层模块只做例化(调用其它模块),不做逻辑。顶层下面会有模块 A、模块 B、模块 C 等,模块 A/B/C 又可以分多个子模块实现。

![0](/images/fpga/0.png)

在这里有一个概念，Verilog语法中的模块例化。FPGA逻辑设计中通常是一个大的模块中包含了一个或多个功能子模块，Verilog通过模块调用或成为模块实例化的方式来实现这些子模块与高层模块的连接，有利于简化每一个模块的代码，易于维护和修改。下面用一个实例（静态数码管显示实验）来说明模块和模块之间的例化方法。在静态数码管显示实验中，我们根据功能将FPGA顶层例化了以下两个模块：计时模块（time_count）和数码管静态显示模块（seg_led_static），如下图显示：																			![1](/images/fpga/1.png)

计时模块部分代码如下：

```verilog
module time_count(
    input	clk,
    input	rst_n,

    output reg flag
);

    parameter MAX_NUM = 25000_000;

    //.........
endmodule

//数码管静态显示模块部分代码如下所示：
module seg_led_static(
    input	clk	,
    input	rst_n	,
    
    input	add_flag,
    output	reg	[5:0]	sel,
    output	reg	[7:0]	seg_led	
);
    
    //.........
endmodule

//顶层模块代码如下
module seg_led_static_top(
	input	sys_clk,
    input	sys_rst_n,
    
    output	[5:0]	sel,
    output	[7:0]	seg_led
);
    
    //parameter define
    parameter TIME_SHOW = 25'd25000_000;
    wire add_flag;
    
    //main code
   
    //例化计时模块
    time_count #(
        .MAX_NUM	(TIME_SHOW)
    )	u_time_count(
        .clk		(sys_clk),
        .rst_n	(sys_rst_n),
        
        .flag		(add_flag)
    );
    
    //例化数码管静态显示模块
    seg_led_static useg_led_static(
        .clk		(sys_clk),
        .rst_n		(sys_rst_n),
        .add_flag	(add_flag),
        .sel			(sel),
        .seg_led	(seg_led)
    );
    
endmodule
```

可以分析以下顶层模块是如何例化子模块的，例化方法如下
![2](/images/fpga/2.png)

参数的例化是要在模块名后面加上“#”，表示后面跟着的是参数列表。计时模块定义MAX_NUM和顶层模块的TIME_SHOW都是等于25000_000，而在Verilog语法中的localparam定义的参数是指本地参数，上层模块不可以对localparam定义的参数做例化。
